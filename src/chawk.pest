Program = { SOI ~ ((PatternBlock ~ Separator) | PatternBlock | Separator)* ~ EOI }

Separator = _{ NEWLINE }

PatternBlock = { (Pattern ~ Block) | Pattern | Block }

Pattern = @{ "/" ~ (!"/" ~ ANY)* ~ "/" }

// NOTE(Chris): We explicitly allow for a newline after the opening curly
// bracket to avoid putting the newline directly into the implicit whitespace
// rule. We exclude newline from the implicit whitespace rule because we want
// the newline to actually matter in some non-atomic rules.
Block = { "{" ~ NEWLINE* ~ (Statement ~ Separator{0, 1})* ~ "}" }

Statement = { PrintStatement }
PrintStatement = { "print" ~ Expression }

Expression = { String | ColumnNumber }
String = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
ColumnNumber = @{ "$" ~ Integer }

Integer = ${ ASCII_DIGIT+ }

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* ~ NEWLINE }
