Program = { SOI ~ ((PatternBlock ~ Separator) | PatternBlock | Separator)* ~ EOI }

Separator = _{ NEWLINE | ";" }

PatternBlock = { (Pattern ~ Block) | Pattern | Block }

Pattern = { "BEGIN" | "END" | Expression }

Regex = @{ "/" ~ (!"/" ~ ANY)* ~ "/" }

Block = {
    "{"
    ~ EmptyLine*
    ~ (Statement ~ Separator ~ EmptyLine*)*
    ~ Statement{0, 1}
    ~ EmptyLine*
    ~ "}"
}

EmptyLine = _{ WHITESPACE* ~ NEWLINE }

Statement = {
    PrintStatement | LocalVarStatement | IfStatement
    | WhileStatement | ForStatement | ExpressionStatement
    | Block 
}
PrintStatement = { "print" ~ Expression }
ExpressionStatement = { Expression }
LocalVarStatement = { "local" ~ Id ~ ("=" ~ Expression){0, 1} }
IfStatement = { "if" ~ "(" ~ Expression ~ ")" ~ Statement ~ ("else" ~ Statement){0, 1} }
WhileStatement = { "while" ~ "(" ~ Expression ~ ")" ~ Statement }
ForStatement = {
    "for"
    ~ "("
    ~ (Expression | LocalVarStatement){0, 1}
    ~ Semicolon
    ~ Expression{0, 1}
    ~ Semicolon
    ~ Expression{0, 1}
    ~ ")"
    ~ Statement
}

Semicolon = { ";" }

// Not all possible operators have been implemented.
// These operators largely match the precedence table from the POSIX awk
// specification (https://pubs.opengroup.org/onlinepubs/9699919799/).
// Expression14 = { Expression15 } // Post-increment, post-decrement
// Expression13 = { Expression14 } // Pre-increment, pre-decrement
// Expression12 = { Expression13 } // Exponentiation
// Expression11 = { Expression12 } // Logical not, unary minus, unary plus (!)
Expression10 = { Atom ~ ((TimesSign | DivSign | PercentSign) ~ Atom)* } // Multiplication, division, modulo
Expression9 = { Expression10 ~ ((PlusSign | MinusSign) ~ Expression10)* } // Addition, subtraction
Expression8 = { Expression9 ~ Expression9* } // String concatenation
Expression7 = { // Comparison operators and equality
    Expression8 ~ (
        // We list LessEqualSign before LessThanSign to parse "<=" before trying "<"
        (LessEqualSign | LessThanSign | NotEqualSign | EqualEqualSign | GreaterEqualSign | GreaterThanSign)
        ~ Expression8
    ){0, 1}
}
Expression6 = { Expression7 } // Regular expression matching
Expression5 = { Expression6 } // Array membership
Expression4 = { Expression5 } // Logical AND
Expression3 = { Expression4 } // Logical OR
Expression2 = { Expression3 } // Ternary conditional
Expression1 = { Id ~ (EqualSign | PlusEqualsSign) ~ Expression2 | Expression2 } // Assignment and arithmetic-assignment
Expression = { Expression1 }
PlusSign = { "+" }
MinusSign = { "-" }
TimesSign = { "*" }
DivSign = { "/" }
PercentSign = { "%" }
EqualSign = { "=" }
PlusEqualsSign = { "+=" }
LessThanSign = { "<" }
LessEqualSign = { "<=" }
NotEqualSign = { "!=" }
EqualEqualSign = { "==" }
GreaterThanSign = { ">" }
GreaterEqualSign = { ">=" }
Atom = {
    !("print" | "local" | "if" | "else" | "while")
    ~ (
	String | ColumnNumber | VarLookup | Num | Regex | "(" ~ Expression ~ ")"
    )
}
String = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
ColumnNumber = @{ "$" ~ PositiveInteger }
VarLookup = { Id }
Num = ${ Integer ~ ("." ~ PositiveInteger){0, 1} }

Integer = ${ "-"{0, 1} ~ PositiveInteger }
PositiveInteger = ${ ASCII_DIGIT+ }

Id = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* ~ NEWLINE }
