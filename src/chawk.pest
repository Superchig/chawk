Program = { SOI ~ ((PatternBlock ~ Separator) | PatternBlock | Separator)* ~ EOI }

Separator = _{ NEWLINE }

PatternBlock = { (Pattern ~ Block) | Pattern | Block }

Pattern = @{ "/" ~ (!"/" ~ ANY)* ~ "/" }

Block = { 
    "{"
    ~ (
	Statement ~ Separator{0, 1}
	| (WHITESPACE* ~ NEWLINE)
    )*
    ~ "}"
}

Statement = { PrintStatement | AssignStatement }
PrintStatement = { "print" ~ Expression }
AssignStatement = { Id ~ "=" ~ Expression }

Expression = { Expression2 }
Expression2 = { Expression1 ~ ((PlusSign | MinusSign) ~ Expression1)* }
PlusSign = { "+" }
MinusSign = { "-" }
Expression1 = { Atom ~ ((TimesSign | DivSign) ~ Atom)* }
TimesSign = { "*" }
DivSign = { "/" }
Atom = { String | ColumnNumber | VarLookup | Num | "(" ~ Expression ~ ")" }
String = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }
ColumnNumber = @{ "$" ~ PositiveInteger }
VarLookup = { Id }
Num = { Integer }

Integer = ${ "-"{0, 1} ~ PositiveInteger }
PositiveInteger = ${ ASCII_DIGIT+ }

Id = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

WHITESPACE = _{ " " | "\t" }

COMMENT = _{ "#" ~ (!"\n" ~ ANY)* ~ NEWLINE }
